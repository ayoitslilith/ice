// Copyright (c) ZeroC, Inc.

plugins {
    id 'com.zeroc.ice.slice-tools' apply false
    id 'checkstyle'
    id 'org.openrewrite.rewrite' version '7.3.0'
}

allprojects {
    repositories {
        mavenCentral()
    }
}

subprojects {
    project.ext.topSrcDir = "$rootProject.projectDir/.."

    project.version = iceVersion
    project.group = "com.zeroc"

    apply plugin: 'checkstyle'
    apply plugin: 'java'
    apply plugin: 'com.zeroc.ice.slice-tools'
    apply from: "$rootProject.projectDir/gradle/ice.gradle"

    repositories {
        mavenCentral()
    }

    java {
        // The corresponding -source and -target javac options are suppressed by gradle when
        // --release is set through options.compileArgs (see above).
        sourceCompatibility = "1.${targetJavaRelease}"
        targetCompatibility = "1.${targetJavaRelease}"
    }

    jar {
        manifest {
            attributes("Built-By": "ZeroC, Inc.")
        }
    }

    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:unchecked" << "-Xlint:-options"
        options.encoding = "UTF-8"
        options.deprecation = true
    }

    //makes checkstyle wait for rewrite. hopefully
    tasks.withType(Checkstyle) {
        dependsOn(rewriteDryRun)
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

task dist()
dist.dependsOn(project(":ice").assemble)
dist.dependsOn(project(":glacier2").assemble)
dist.dependsOn(project(":icegrid").assemble)
dist.dependsOn(project(":icebox").assemble)
dist.dependsOn(project(":icebt").assemble)
dist.dependsOn(project(":icediscovery").assemble)
dist.dependsOn(project(":icelocatordiscovery").assemble)
dist.dependsOn(project(":icestorm").assemble)
dist.dependsOn(project(":IceGridGUI").assemble)

project(":test").compileJava.dependsOn(dist)

def exportedProjects = [
    ":glacier2",
    ":ice",
    ":icebox",
    ":icebt",
    ":icegrid",
    ":icestorm"
]

task alljavadoc(type: Javadoc) {
    // Add the source files from all subprojects
    source = exportedProjects.collect { project(it).sourceSets.main.allJava }
    // Output directory for the aggregated Javadocs
    destinationDir = file("$buildDir/docs/javadoc")
    options.encoding = 'UTF-8'
    // Where to find source files for the different modules
    options.addStringOption('-module-source-path', './src/*/src/main/java/:./src/*/build/generated/source/slice/main')
    options.addBooleanOption('html5', true)
    options.header = 'Ice for Java'
    options.docTitle = "Ice ${iceVersion} API Reference"
}
alljavadoc.dependsOn = exportedProjects.collect { project(it).assemble.dependsOn }

checkstyle {
    toolVersion = '10.21.4'
    ignoreFailures = false
    showViolations = true
}

rewrite {
    activeRecipe(
        'org.openrewrite.staticanalysis.DefaultComesLast',
        'org.openrewrite.staticanalysis.EmptyBlock',
        'org.openrewrite.java.format.EmptyNewlineAtEndOfFile',
        'org.openrewrite.staticanalysis.ForLoopControlVariablePostfixOperators',
        //'org.openrewrite.staticanalysis.FinalizePrivateFields',
        'org.openrewrite.java.format.MethodParamPad',
        'org.openrewrite.java.format.NoWhitespaceAfter',
        'org.openrewrite.java.format.NoWhitespaceBefore',
        'org.openrewrite.java.format.PadEmptyForLoopComponents',
        'org.openrewrite.staticanalysis.TypecastParenPad',
        'org.openrewrite.staticanalysis.EqualsAvoidsNull',
        'org.openrewrite.staticanalysis.ExplicitInitialization',
        'org.openrewrite.staticanalysis.FallThrough',
        'org.openrewrite.staticanalysis.HideUtilityClassConstructor',
        'org.openrewrite.staticanalysis.NeedBraces',
        'org.openrewrite.staticanalysis.OperatorWrap',
        'org.openrewrite.staticanalysis.UnnecessaryParentheses',
        'org.openrewrite.staticanalysis.ReplaceThreadRunWithThreadStart',
        'org.openrewrite.staticanalysis.ChainStringBuilderAppendCalls',
        'org.openrewrite.staticanalysis.ReplaceStringBuilderWithString',
        //'org.openrewrite.java.ShortenFullyQualifiedTypeReferences',
    )
    checkstyleConfigFile = file("config/checkstyle/checkstyle.xml")
    setExportDatatables(true)
    exclusion(
        '**/*.gradle',
        'com/zeroc/Ice/PropertyNames.java',
        '**/*.kts'
    )
}

dependencies {
    rewrite("org.openrewrite.recipe:rewrite-static-analysis:2.5.1")
}
